apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def grpcVersion = '1.20.0'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def artifactory_user = properties.getProperty("artifactory_user")
def artifactory_password = properties.getProperty("artifactory_password")

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

android {
    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir '../protos'
            }
        }
    }

    compileSdkVersion 29

    defaultConfig {
        applicationId "edu.cmu.cs.openrtist"
        targetSdkVersion 29
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "version"
    productFlavors {
        standard {
            isDefault = true
            minSdkVersion 24
        }
        measurement {
            minSdkVersion 24
        }
        mobilEdgeX {
            apply plugin: 'com.jfrog.artifactory'

            minSdkVersion 24
            repositories {
                maven {
                    credentials {
                        // Create these variables in local.properties if you don't have them.
                        username artifactory_user
                        password artifactory_password
                    }
                    url = "https://artifactory.mobiledgex.net/maven-releases/"
                }
            }
        }
    }

    protobuf {
        protoc {
            // You still need protoc like in the non-Android case
            artifact = 'com.google.protobuf:protoc:3.7.0'
        }
        plugins {
            javalite {
                // The codegen for lite comes as a separate artifact
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    // In most cases you don't need the full Java output
                    // if you use the lite output.
                    remove java
                }
                task.plugins {
                    javalite {
                        outputSubDir = 'java'
                    }
                }

            }
        }
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'edu.cmu.cs.gabriel:client:2.1.0'

    implementation 'org.pytorch:pytorch_android:1.4.0-SNAPSHOT'
    implementation 'org.pytorch:pytorch_android_torchvision:1.4.0-SNAPSHOT'

    // Allow user to build other flavors when they do not have MobilEdgeX credentials
    if (artifactory_user != null) {
        mobilEdgeXImplementation 'com.mobiledgex:matchingengine:1.4.11'
    }

    mobilEdgeXImplementation "io.grpc:grpc-okhttp:${grpcVersion}"
    mobilEdgeXImplementation "io.grpc:grpc-stub:${grpcVersion}"
    mobilEdgeXImplementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    mobilEdgeXImplementation 'edu.cmu.cs.gabriel:protocol-for-mobiledgex:2.0.1'

    standardImplementation 'edu.cmu.cs.gabriel:protocol-old-protobuf:2.0.1'
    measurementImplementation 'edu.cmu.cs.gabriel:protocol-old-protobuf:2.0.1'
}
