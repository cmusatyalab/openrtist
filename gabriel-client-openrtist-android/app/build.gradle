apply plugin: 'com.android.application'

def grpcVersion = '1.20.0'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "edu.cmu.cs.openrtist"
        targetSdkVersion 28
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "version"
    productFlavors {
        standard {
            minSdkVersion 19
        }
        mobileEdgeX {
            apply plugin: 'com.jfrog.artifactory'

            minSdkVersion 23
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def artifactory_user = properties.getProperty("artifactory_user")
            def artifactory_password = properties.getProperty("artifactory_password")

            repositories {
                maven {
                    credentials {
                        // Create these variables in local.properties if you don't have them.
                        username artifactory_user
                        password artifactory_password
                    }
                    url = "https://artifactory.mobiledgex.net/maven-releases/"
                }
            }
        }
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation('edu.cmu.cs.gabriel:client:0.1.6-SNAPSHOT') {
        exclude group: 'com.google.protobuf'
    }

    mobileEdgeXImplementation 'com.mobiledgex:matchingengine:1.4.11'
    mobileEdgeXImplementation "io.grpc:grpc-okhttp:${grpcVersion}"
    mobileEdgeXImplementation "io.grpc:grpc-stub:${grpcVersion}"
    mobileEdgeXImplementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
}

