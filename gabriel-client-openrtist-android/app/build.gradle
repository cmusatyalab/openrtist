apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def grpcVersion = '1.20.0'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def artifactory_user = properties.getProperty("artifactory_user")
def artifactory_password = properties.getProperty("artifactory_password")

android {
    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir '../protos'
            }
        }
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "edu.cmu.cs.openrtist"
        targetSdkVersion 28
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "version"
    productFlavors {
        standard {
            minSdkVersion 19
        }
        timing {
            minSdkVersion 19
        }
        mobilEdgeX {
            apply plugin: 'com.jfrog.artifactory'

            minSdkVersion 23
            repositories {
                maven {
                    credentials {
                        // Create these variables in local.properties if you don't have them.
                        username artifactory_user
                        password artifactory_password
                    }
                    url = "https://artifactory.mobiledgex.net/maven-releases/"
                }
            }
        }
    }

    protobuf {
        protoc {
            // You still need protoc like in the non-Android case
            artifact = 'com.google.protobuf:protoc:3.7.0'
        }
        plugins {
            javalite {
                // The codegen for lite comes as a separate artifact
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    // In most cases you don't need the full Java output
                    // if you use the lite output.
                    remove java
                }
                task.plugins {
                    javalite {
                        outputSubDir = 'java'
                    }
                }

            }
        }
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'edu.cmu.cs.gabriel:client:0.1.7-SNAPSHOT'

    // Allow user to build other flavors when they do not have MobilEdgeX credentials
    if (artifactory_user != null) {
        mobilEdgeXImplementation 'com.mobiledgex:matchingengine:1.4.11'
    }

    mobilEdgeXImplementation "io.grpc:grpc-okhttp:${grpcVersion}"
    mobilEdgeXImplementation "io.grpc:grpc-stub:${grpcVersion}"
    mobilEdgeXImplementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    mobilEdgeXImplementation 'edu.cmu.cs.gabriel:protocol-for-mobiledgex:0.1.18-SNAPSHOT'

    standardImplementation 'edu.cmu.cs.gabriel:protocol-old-protobuf:0.1.18-SNAPSHOT'
    timingImplementation 'edu.cmu.cs.gabriel:protocol-old-protobuf:0.1.18-SNAPSHOT'
}
